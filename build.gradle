
plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}


group = "mekaweapons"
version = "1.19.2-${mod_version}"

base {
    archivesName = "mekaweapons"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'parchment', version: '2022.08.14-1.19.2'
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                "mekaweapons" {
                    source sourceSets.main
                }
            }
        }

        client { property 'forge.enabledGameTestNamespaces', "mekaweapons" }

        server {
            property 'forge.enabledGameTestNamespaces', "mekaweapons"
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', "mekaweapons"
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', "mekaweapons", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://modmaven.dev/"
        content { includeGroup("mekanism") }
    }
    maven {
        url "https://cursemaven.com"
        content { includeGroup("curse.maven") }
    }
    maven {
        url 'https://maven.blamejared.com'
        content { includeGroup("mezz.jei") }
    }
    maven { url = "https://maven.theillusivec4.top/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation(fg.deobf("mekanism:Mekanism:${minecraft_version}-${mekanism_version}"))
    compileOnly(fg.deobf("mekanism:Mekanism:${minecraft_version}-${mekanism_version}:api"))
    compileOnly(fg.deobf("mekanism:Mekanism:${minecraft_version}-${mekanism_version}:generators"))
    compileOnly(fg.deobf("top.theillusivec4.curios:curios-forge:1.19.2-5.1.4.3:api"))
    compileOnly(fg.deobf("curse.maven:ad-astra-giselle-addon-714958:4819640"))

    runtimeOnly fg.deobf("mezz.jei:jei-1.19.2-forge:11.6.0.1018")
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: "mekaweapons", mod_name: "Meka Weapons", mod_license: "MIT", mod_version: "${mod_version}",
            mod_authors: "oMeranha", mod_description: "Weapons for Mekanism.",
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title": "mekaweapons",
                "Specification-Vendor": "oMeranha",
                "Specification-Version": "${mod_version}",
                "Implementation-Title": "mekaweapons",
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"oMeranha",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
